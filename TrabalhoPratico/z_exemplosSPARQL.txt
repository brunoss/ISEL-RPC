#_________________________________________________________________________
# Paulo Trigo Silva (PTS)
# SPARQL (exemplos de apoio ao trabalho prático)
# Notar que algumas interrogações podem demorar tempo a obter resposta
# (teste as interrogações incrementalmente
# para perceber qual, ou quais, as cláusulas que mais tempo consomem)
#_________________________________________________________________________



#_________________________________________________________________________
#_________________________________________________________________________
# interrogações sobre a DBLP, executadas em:
# http://dblp.rkbexplorer.com/sparql
#_________________________________________________________________________
#_________________________________________________________________________



#_________________________________________________________________________
# obter as sub-classes de dblp:Publication-Reference
# The distinction between publication and publication-reference
# makes it possible to distinguish between multiple occurrences
# of the same publication, for instance in different media
# c.f., http://www.aktors.org/ontology/portal#article-of-journal
# os indivíduos pertencem às sub-classes de dblp:Publication-Reference
PREFIX dblp:  <http://swat.cse.lehigh.edu/resources/onto/dblp.owl#>

SELECT DISTINCT ?sub_type ?o
WHERE 
   { 
   ?sub_type rdfs:subClassOf ?o .
   }
LIMIT 50



# RESPOSTA:
# dblp:Publication-Reference
# dblp:Article-Reference
# dblp:Book-Reference
# dblp:Conference-Proceedings-Reference
# dblp:Thesis-Reference
# dblp:Book-Section-Reference
### as próximas classes não têm indivíduos (em DBLP)
# dblp:Web-Reference
# dblp:Workshop-Proceedings-Reference
# dblp:Proceedings-Paper-Reference
# dblp:Technical-Report-Reference
# dblp:Edited-Book-Reference

# Para verificar se determinada classe (e.g., dblp:Web-Reference) tem alguma instância pode executar:
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dblp:  <http://swat.cse.lehigh.edu/resources/onto/dblp.owl#>
 
SELECT DISTINCT ?instance
WHERE 
   { 
   ?instance rdf:type dblp:Article-Reference .
   }
LIMIT 50
#_________________________________________________________________________



=====//||\\=====



#_________________________________________________________________________
# BOOK
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dblp:  <http://swat.cse.lehigh.edu/resources/onto/dblp.owl#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
  
SELECT DISTINCT ?pub ?pub_title ?author ?author_full_name ?pub_date ?pub_volume ?pub_web_address ?same_as
WHERE 
   {
   ?pub rdf:type dblp:Book-Reference .
   ?pub dblp:has-title ?pub_title .
   ?pub dblp:has-author ?author .
   ?pub dblp:has-date ?pub_date .
   ?pub dblp:has-volume ?pub_volume .
   ?pub dblp:has-web-address ?pub_web_address .
   ?author rdf:type dblp:Person .
   ?author dblp:full-name ?author_full_name .

   OPTIONAL { ?author dblp:has-affiliation ?author_affiliation . 
              ?author_affiliation dblp:has-pretty-name ?author_affiliation_name . } .
   OPTIONAL { ?pub owl:sameAs ?same_as } .
   }
LIMIT 20
#_________________________________________________________________________




=====//||\\=====



#_________________________________________________________________________
# ARTICLE-IN-JOURNAL
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dblp:  <http://swat.cse.lehigh.edu/resources/onto/dblp.owl#>
  
SELECT DISTINCT ?pub ?pub_title ?author ?author_full_name ?author_affiliation_name ?pub_date ?pub_volume ?pub_web_address ?pub_type ?pub_type_title ?same_as
WHERE 
   {
   ?pub rdf:type dblp:Article-Reference .
   ?pub dblp:article-of-journal ?pub_type .
   ?pub_type dblp:has-title ?pub_type_title .
   ?pub dblp:has-title ?pub_title .
   ?pub dblp:has-date ?pub_date .
   ?pub dblp:has-volume ?pub_volume .
   ?pub dblp:has-web-address ?pub_web_address .
   ?pub dblp:has-author ?author .
   ?author rdf:type dblp:Person .
   ?author dblp:full-name ?author_full_name .

   OPTIONAL { ?author dblp:has-affiliation ?author_affiliation . 
              ?author_affiliation dblp:has-pretty-name ?author_affiliation_name . } .
   }
LIMIT 20

## RETIREI (DA INTERROGAÇÃO ACIMA) PARA EVITAR ORIGINAR TUPLOS DUPLICADOS:
   OPTIONAL { ?pub owl:sameAs ?same_as } .
#_________________________________________________________________________



=====//||\\=====



#_________________________________________________________________________
# ARTICLE-IN-PROCEEDINGS
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX dblp:  <http://swat.cse.lehigh.edu/resources/onto/dblp.owl#>
  
SELECT DISTINCT ?pub ?pub_title ?author ?author_full_name ?pub_date ?pub_type ?pub_type_title ?pub_journal_type_title ?pub_journal_type ?pub_web_address ?same_as
WHERE 
   {
   ?pub_type rdf:type dblp:Conference-Proceedings-Reference .
   ?pub_type dblp:has-title ?pub_type_title .
   ?pub dblp:cites-publication-reference ?pub_type .
   ?pub dblp:has-title ?pub_title .
   ?pub dblp:has-date ?pub_date .
   ?pub dblp:has-web-address ?pub_web_address .
   ?pub dblp:has-author ?author .
   ?author rdf:type dblp:Person .
   ?author dblp:full-name ?author_full_name .

   OPTIONAL { ?author dblp:has-affiliation ?author_affiliation . 
              ?author_affiliation dblp:has-pretty-name ?author_affiliation_name . } .

   OPTIONAL { ?pub dblp:article-of-journal ?pub_journal_type .
              ?pub_journal_type dblp:has-title ?pub_journal_type_title } .

   OPTIONAL { ?pub owl:sameAs ?same_as } .
   }
LIMIT 20
#_________________________________________________________________________



=====//||\\=====



#_________________________________________________________________________
# AUTHOR-AFFILIATION-PUBLICATION (dada uma determinada parte de um nome)
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dblp:  <http://swat.cse.lehigh.edu/resources/onto/dblp.owl#>

SELECT DISTINCT ?author ?author_name ?author_affiliation ?author_affiliation_name ?pub ?pub_title
WHERE
   {
   ?author rdf:type dblp:Person .
   ?author dblp:full-name ?author_name .
   FILTER( regex( str( ?author_name ), "Dijkstra" ) )

   ?pub dblp:has-author ?author .
   ?pub dblp:has-title ?pub_title .

   OPTIONAL{ ?author dblp:has-affiliation ?author_affiliation .
             ?author_affiliation dblp:has-pretty-name ?author_affiliation_name } .
   }
LIMIT 20

#_________________________________________________________________________

## OUTRO NOME DE AUTOR:
   FILTER( regex( str( ?author_name ), "Griffeth" ) )